import os
import sys

Import("env")
env = env.Clone()

# increase line length of LuaLaTeX error log lines by setting environment variables
env["ENV"]["max_print_line"] = "1000"
env["ENV"]["error_line"] = "254"
env["ENV"]["half_error_line"] = "238"

# use LuaLaTeX as compiler (successor of PDFLaTeX)
env.Replace(PDFLATEX="texfot lualatex")
# enable SyncTeX for GUI editors
env.Append(PDFLATEXFLAGS="--synctex=1")
# enable shell commands to display Git version
env.Append(PDFLATEXFLAGS="--shell-escape")

# don't call BibTeX on *.aux files of chapters
env.Replace(BIBTEXCOM="")
# quiet Biber output
env.Append(BIBERFLAGS="-q")

# use customized makeindex script
env.Replace(MAKEGLOSSARY="python3 ../../tools/buildGlossary.py")
# quiet output
env.Replace(MAKEGLOSSARYFLAGS="-q")
# reorder arguments
# (filename without extension has to be at the end)
env.Replace(MAKEGLOSSARYCOM=env["MAKEGLOSSARYCOM"].replace(
    "${SOURCE.filebase}.glo $MAKEGLOSSARYFLAGS -o ${SOURCE.filebase}.gls",
    "$MAKEGLOSSARYFLAGS -o ${SOURCE.filebase}.gls ${SOURCE.filebase}"))

# set TMPDIR to get texfot to work on Windows
if sys.platform == "win32": env["ENV"]["TMPDIR"] = env["ENV"]["TEMP"]

# remove temporary files created accidential run of LuaLaTeX instead of SCons
# (pressing the wrong button in TeXstudio)
for path in os.listdir("."):
  name, ext = os.path.splitext(path)
  
  if ext == ".tex":
    for extension in ["acn", "aux", "bbl", "bcf", "blg", "glo", "ist", "lof", "log", "lot", "nlo",
                      "out", "pdf", "run.xml", "synctex.gz", "thm", "toc", "uhy", "upa", "upb"]:
      path2 = "{}.{}".format(name, extension)
      if os.path.isfile(path2): os.remove(path2)

# set output directory
env.Append(PDFLATEXFLAGS="--output-directory={}".format(env["BUILD_DIR"]))

# compile PDF
pdf = env.PDF(target="{}/thesis.pdf".format(env["BUILD_DIR"]), source="thesis.tex")

# execute Lua SConscript
lua = env.SConscript("../lua/SConscript", exports="env")
env.Depends(pdf, lua)

# force glossary generation for each run
# (the glossary does not seem to update if the description of a
# glossary entry changes; you would have to change the name of the entry
# to force re-generation...)
def delete_glossary_files(target, source, env):
  for extension in ["gls", "glsdefs", "glo"]:
    path = os.path.join(env["BUILD_DIR"].abspath, "thesis.{}".format(extension))
    env.Execute(Delete(path))

env.AddPreAction(pdf, delete_glossary_files)

# copy hyphenation whitelist (needed in check mode)
hyphenation_whitelist = env.Install(env["BUILD_DIR"], "hyphenation_whitelist.txt")
env.Depends(pdf, hyphenation_whitelist)

Return("pdf")
